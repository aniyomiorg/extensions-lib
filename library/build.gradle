import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.dokka.DokkaConfiguration.Visibility

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {
    compileSdk 34

    defaultConfig {
        versionName '14'
        minSdk 21
        targetSdk 34
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

dependencies {
    compileOnly("com.squareup.okhttp3:okhttp:5.0.0-alpha.11")
    compileOnly("io.reactivex:rxjava:1.3.8")
    compileOnly("io.reactivex:rxandroid:1.2.1")
    compileOnly("org.jsoup:jsoup:1.16.1")
    compileOnly("com.github.inorichi.injekt:injekt-core:65b0440")

    compileOnly("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1")
    def json = "1.5.1"
    compileOnly("org.jetbrains.kotlinx:kotlinx-serialization-json:$json")
    compileOnly("org.jetbrains.kotlinx:kotlinx-serialization-json-okio:$json")
}

task androidSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.withType(DokkaTask.class) {
    dokkaSourceSets {
        named("main") {
            moduleName.set("extensions-lib")
            moduleVersion.set(android.defaultConfig.versionName)
            outputDirectory.set(file("build/docs/"))
            // Speedup doc generation
            // offlineMode.set(true)
            includes.from("Module.md")

            perPackageOption {
                matchingRegex.set("android.content")
                suppress.set(true)
            }

            documentedVisibilities.set([
                Visibility.PUBLIC,
                Visibility.PROTECTED
            ])

            externalDocumentationLink {
                url.set(new URL("https://square.github.io/okhttp/4.x/"))
                // https://github.com/square/okhttp/issues/7338
                packageListUrl.set(new URL("https://colinwhite.me/okhttp3-package-list"))
            }

            externalDocumentationLink {
                url.set(new URL("https://jsoup.org/apidocs/"))
                packageListUrl.set(new URL("https://jsoup.org/apidocs/element-list"))
            }

            externalDocumentationLink {
                url.set(new URL("https://reactivex.io/RxJava/1.x/javadoc/"))
            }
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
            }
        }
    }
}
