import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionName '14'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.11"
    implementation "io.reactivex:rxjava:1.3.8"
    implementation "io.reactivex:rxandroid:1.2.1"
    implementation "org.jsoup:jsoup:1.15.3"
    implementation "com.github.inorichi.injekt:injekt-core:65b0440"
}

task androidSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

tasks.withType(DokkaTask.class) {
    dokkaSourceSets {
        named("main") {
            moduleName.set("extensions-lib")
            moduleVersion.set(android.defaultConfig.versionName)
            outputDirectory.set(file("build/docs/"))
            offlineMode.set(true)
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
            }
        }
    }
}
